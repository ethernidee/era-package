ZVSE2

!?FU(era_TestStringsInterpolation);
!!FU(et_EnterTest):P^Test strings interpolation^;

!!VRy7:S5;
!!VRy5:S36963;
!!VRz2:S^%%y%y7^;
!!VRy5:S24842;
!!FU(et_ExpectStr):Pz2/^%%y5^/^Regular strings interpolation must not be recursive^/(FILE)/(LINE)/(CODE);
; ----------------------------
!!VRy7:S5;
!!VRy5:S36963;
!!VRz2:S^%%y%y7^;
!!VRy5:S24842;
!!FU(et_ExpectStr):Pz2/^%%y5^/^Non ERT z-strings must not be automatically interpolated^/(FILE)/(LINE)/(CODE);

!!FU(et_LeaveTest):P;


!?FU(era_TestNativeMacroVars);
!!FU(et_EnterTest):P^Test native macro variables^;

!!FU(et_ErrorsOff):P;
!!FU(et_Dummy):P$wtf_o_O$;
!!FU(et_ExpectError):P^Unknown macro name evaluation must trigger error^/(FILE)/(LINE)/(CODE);
; ----------------------------

!!FU(et_LeaveTest):P;


; ======================================
!?FU(era_TestFunctionsSupport);
!!FU(et_EnterTest):P^Test functions support^;

!!VRx1:C-999/-999/-999/-999/-999/-999/-999/-999/-999/-999/-999/-999/-999/-999/-999/-999;
!!FU(era_TestDefaultArgsInitialization):P100/200;
; ----------------------------
!!VRx1:C-999/-999/-999/-999/-999/-999/-999/-999/-999/-999/-999/-999/-999/-999/-999/-999;
!!DO(era_TestDefaultArgsInitializationInLoop)/444/444/1:P100/200;

!!FU(et_LeaveTest):P;


!?FU(era_TestDefaultArgsInitialization);
!!FU(et_EnterTest):P^Test default function parameters initialization^;

!!FU(et_Fail)|x1<>100/x2<>200:P^Default initialization must not override passed function parameters^/(FILE)/(LINE)/(CODE)/^Expected x1 = 100, x2 = 200. Given: %x1, %x2^;
!!FU(et_Fail)|x3<>0/x4<>0/x5<>0/x6<>0/x7<>0/x8<>0/x9<>0/x10<>0/x11<>0/x12<>0/x13<>0/x14<>0/x15<>0/x16<>0:P^Arguments must be initialized with 0 by default^/
  (FILE)/(LINE)/(CODE)/^Got x3..x16: %x3, %x4, %x5, %x6, %x7, %x8, %x9, %x10, %x11, %x12, %x13, %x14, %x15, %x16^;

!!FU(et_LeaveTest):P;


!?FU(era_TestDefaultArgsInitializationInLoop);
!!FU(et_EnterTest):P^Test default function parameters initialization in loops^;

!!FU(et_Fail)|x1<>100/x2<>200:P^Default initialization must not override passed function parameters^/(FILE)/(LINE)/(CODE)/^Expected x1 = 100, x2 = 200. Given: %x1, %x2^;
!!FU(et_Fail)|x3<>0/x4<>0/x5<>0/x6<>0/x7<>0/x8<>0/x9<>0/x10<>0/x11<>0/x12<>0/x13<>0/x14<>0/x15<>0/x16<>444:P^Arguments must be initialized with 0 by default. x16 must not be touched^/
  (FILE)/(LINE)/(CODE)/^Got x3..x16: %x3, %x4, %x5, %x6, %x7, %x8, %x9, %x10, %x11, %x12, %x13, %x14, %x15, %x16^;

!!FU(et_LeaveTest):P;
; ======================================


!?FU(era_TestHeroSpecsSupport);
!!FU(et_EnterTest):P^Test HE:X^;

!!HE10:X0/1/2/3/4/5/6;
!!HE10:X?y1/?y2/?y3/?y4/?y5/?y6/?y7;
!!FU(et_Fail)|y1<>0/y2<>1/y3<>2/y4<>3/y5<>4/y6<>5/y7<>6:P^HE:X with 7 arguments produces wrong result^/(FILE)/(LINE)/(CODE)/^Expected: 0/1/2/3/4/5/6. Got: %y1/%y2/%y3/%y4/%y5/%y6/%y7^;
; ----------------------------
!!HE10:X0/0/0/0/0/0/0;
!!HE10:X6/50/51/13;
!!HE10:X?y1/?y2/?y3/?y4/?y5/?y6/?y7;
!!FU(et_Fail)|y1<>6/y2<>50/y6<>51/y7<>13:P^HE:X6 produces wrong result^/(FILE)/(LINE)/(CODE)/^Expected: 6/50/51/13. Got: %y1/%y2/%y6/%y7^;
; ----------------------------
!!HE10:X0/0/0/0/0/0/0;
!!HE10:X7/33/66;
!!HE10:X?y1/?y2/?y3/?y4/?y5/?y6/?y7;
!!FU(et_Fail)|y1<>7/y3<>33/y4<>66:P^HE:X7 produces wrong result^/(FILE)/(LINE)/(CODE)/^Expected: 7/33/66. Got: %y1/%y3/%y4^;

!!FU(et_LeaveTest):P;


!?FU(era_TestSnMem);
!!FU(et_EnterTest):P^Test SN:M^;

!!VRv1:S999;
!!SN:M(M_AUTO_ID)/3/(M_INT)/(M_TRIGGER_LOCAL)/?(array:y);
!!FU(et_ExpectInt):Pv1/999/^v1 was changed even when using SN:M with 5 parameters^/(FILE)/(LINE)/(CODE);
; ----------------------------

!!FU(et_LeaveTest):P;

!?PI;
!!FU(era_TestStringsInterpolation):P;
!!FU(era_TestNativeMacroVars):P;
!!FU(era_TestFunctionsSupport):P;
!!FU(era_TestHeroSpecsSupport):P;
!!FU(era_TestSnMem):P;