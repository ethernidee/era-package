ZVSE
** [Mysterious Creature Dwelling]
** Mysterious Creature Dwelling 1.3 - Dieter Averbeck 
** This Script gives the a visiting Heroe randomly 
** 7 creatures in additional to the heroe. 
** Player can revisit this builing once every 10 days.

** To use this script, just copy and paste it into a 
** timed event, that not will be shown. For example Day 500. 
** Then place one or more of the object 3 on the map  
** (It`s the 1st building after the towns).
** Variables used: v141,v142,v143,v144,z142,z143, FU142, flag 143

!#VRv141:S1;
!#OW:C?v144;

!#TM14:S1/999/10/255;


!?TM14;
!!VRv141:S1;

!#VRz142:S^Mysterious Creature Dwelling^;
!#VRz143:S^Mysterious Creature Dwelling (empty)^;
!#HT63/3:V141/1/142; 
!#HT63/3:V141/0/143; 

!?OB63/3;
!!IF&1000/v141<>1:M^All creatures are gone. Perhaps you come back later!^;

!!FU142&1000/v141=1:P;


!?FU142;
!!HE-1:N?y1;
!!VRy2&y1<16:S0; ** Castle Creatures
!!VRy2&y1>15/y1<32:S14; ** Rampart Creatures
!!VRy2&y1>31/y1<48:S28; ** Tower Creatures
!!VRy2&y1>47/y1<64:S42; ** Inferno Creatures
!!VRy2&y1>63/y1<80:S56; ** Necro Creatures
!!VRy2&y1>79/y1<96:S70; ** Dungeon Creatures
!!VRy2&y1>95/y1<112:S84; ** Stronghold Creatures
!!VRy2&y1>111/y1<128:S98; ** Foretress Creatures
!!VRy10&y1>127/y1<144:S1; ** neutral/Conflux Creatures
!!VRy2&y1=144:S0; ** AddON Heroes
!!VRy2&y1=146:S0;
!!VRy2&y1=152:S0;
!!VRy2&y1=148:S14;
!!VRy2&y1=147:S28;
!!VRy2&y1=155:S42;
!!VRy2&y1=150:S56;
!!VRy2&y1=151:S70;
!!VRy2&y1=153:S70;
!!VRy2&y1=149:S84;
!!VRy2&y1=154:S84;
!!VRy2&y1=145:S98;

** Calculate a random Monster and cost for 7 slots

!!VRy28:S0;

!!VRy3:S0 R13;
!!VRy11:Sy3; !!VRy11::2; 
!!VRy11:*-1; !!VRy11:+7;
!!VRy3:+y2;

!!VRy4:S0 R13;
!!VRy12:Sy4; !!VRy12::2; 
!!VRy12:*-1; !!VRy12:+7;  
!!VRy4:+y2;

!!VRy5:S0 R13;
!!VRy13:Sy5; !!VRy13::2; 
!!VRy13:*-1; !!VRy13:+7; 
!!VRy5:+y2;
 
!!VRy6:S0 R13;
!!VRy14:Sy6; !!VRy14::2; 
!!VRy14:*-1; !!VRy14:+7; 
!!VRy6:+y2;
 
!!VRy7:S0 R13;
!!VRy15:Sy7; !!VRy15::2; 
!!VRy15:*-1; !!VRy15:+7;
!!VRy7:+y2;
  
!!VRy8:S0 R13;
!!VRy16:Sy8; !!VRy16::2; 
!!VRy16:*-1; !!VRy16:+7;  
!!VRy8:+y2;

!!VRy9:S0 R13;
!!VRy17:Sy9; !!VRy17::2; 
!!VRy17:*-1; !!VRy17:+7;
!!VRy9:+y2;  

** Calculate the cost of all monsters together

!!MA:Cy3/6/?y21;
!!VRy21:*y11;
!!VRy28:+y21;
!!MA:Cy4/6/?y22;
!!VRy22:*y12;
!!VRy28:+y22;
!!MA:Cy5/6/?y23;
!!VRy23:*y13;
!!VRy28:+y23;
!!MA:Cy6/6/?y24;
!!VRy24:*y14;
!!VRy28:+y24;
!!MA:Cy7/6/?y25;
!!VRy25:*y15;
!!VRy28:+y25;
!!MA:Cy8/6/?y26;
!!VRy26:*y16;
!!VRy28:+y26;
!!MA:Cy9/6/?y27;
!!VRy27:*y17;
!!VRy28:+y27;

!!IF&y10<>1:Q143/6/y28/2^This Mysterious Creature Dwelling offers you only one time per week 7 creatures to join your army. This time you would have to pay %Y28 gold. Do you want to buy some?^;

!!IF&-143/y10<>1:M^Perhaps you come back again later.^;

!!IF&y10=1:M^Conflux Heroes can`t use this dwelling^;


!!OW:R-1/6/?y29;
!!IF&143/y29<=y28:Q144/6/y28/1^The creatures cost %Y28 gold this time. You don`t have enough money. Come back later!^;
!!VRv141&y29<=y28:S1;

!!HE-1&143/y10<>1/y29>=y28:Cy3/y11/y4/y12/y5/y13/y6/y14/y7/y15/y8/y16/y9/y17;

!!VRy30:Sy29;
!!VRy30:-y28;
!!OW&143/y10<>1/y29>=y28:R-1/6/y30;

!!VRv141&143/y29>=y28:S0;
