ZVSE
!#IF:V888/0;
!#IF:V889/0;
!#IF:V890/0;

!?OB63/20/0;
!!IF:V1/0;
!!IF&-889:Q1^The doors of this house are standing wide open. You get the feeling something sinister is lurking on the inside. Do you wish to take a look?^;
!!IF&1/-889:M^When you stick your head into it to take a look, you feel a strong pull, and you find yourself facing a group of strange creatures and humans.
They attack!^;
!!IF&-1:M^You decide that discretion is the better part of valor and quickly leave.^;

!!IF:V890/0;
!!IF&-889:V888/1;
!!HE-1:R4/0;
!!HE-1&1:T63/20/0/74/1; (provoke battle)
!!HE-1:R4/-1;

!!OW:A-1/?y1;

!!IF&-888:M^The house is now empty, with no traces of the battle to be seen.^;
!!FU2&y1=0/888:P;

!?FU2;
!!IF:Q1^After the battle, you search the house and find asome strange robes, that seems to be able to give the other mages in the partya similar protection as the mage you just fought, as well as an odd ring. It emanetes strong magics, but it doesn't seem to be entirely beneficial, and you don't think you can remove it after donning it. Do you wish to do so anyway?^;

!!IF&1:M^When you don the ring, you feel sick, and pass out for a moment. When you wake up, you feel much stronger, but also as if your magic has grown weaker!^;
!!HE-1&1:Fd3/d3/d-2/d-2;

!!IF:V888/0;
!!IF:V889/1;

!?BA0;
!!FU4&888:P;

!?FU4;
!!BA:B12; (subterrean background)
!!BA:H1/94; (setup defending hero)

!?BF&888;
!!BF&888:C; (clear battlefield)
!!BU&-890:S34/1/15/1/-1/0;
!!BU&-890:S6/1/49/1/-1/0;
!!BU&-890:S63/1/151/1/-1/0;
!!BU&-890:S64/1/185/1/-1/0; (summon stacks)
!!IF:V890/1;

!?BR;
!!FU11&888/v997=0:P;

!?FU11;
!!VRv1:S0;
!!VRv2:S0;
!!DO1/20/28/1:P34;   (loop through all stacks)
!!BMv2&v1<>0:M29/99/2; (cast Fire Shield on mage)

!!VRv1:S0;
!!VRv2:S0;

!!DO1/20/28/1:P64;   (loop through all stacks)
!!BMv2&v1<>0:M46/2/99; (cast Stone Skin on lich)

!?FU1;
!!VRy2:S0;
!!BMx16:T?y1; (Get creature)
!!BMx16&y1=x1:N?y2; (Get number)
!!BMx16&y2=1/y1=x1:P?v1; (Get position)
!!VRv2&y2=1/y1=x1:Sx16;

!?BG;
!!FU12&888:P;

!?FU12;
!!VRy1:S0;
!!VRy2:S-1;
!!VRy3:S0;
!!VRy4:S0;
!!VRy5:S20;

!!BG:A?y1;  (get type of action taken)
!!BG&y1=7:D?v32; (get target)
!!BG&y1=7/v32><-1:N?v30; (get stack that shot)
!!BMv30&y1=7/v32><-1:T?y4; (get what type of creature that shot)
!!VRy5&y1=7/v32><-1/y4=74:T6; 
!!VRv20:C15/42/45/47/50/52/54;
!!VRv31&y1=7/v32><-1/y4=74:Svy5;
!!IF&y1=7/v32><-1/y4=74:V887/1;

!?BG1&887;
!!BMv30&v31<>15:Cv31/v32/1/5/1;  (cast spell-not Magic Arrow) 
!!BMv30&v31=15:Cv31/v32/1/1/1;  (cast Magic Arrow) 
!!BG:N?y1;  (current stack)
!!BMy1:C2/v31/0/0/0;  (void spell to restore correct creatures turn)
!!VRv30:S0;
!!VRv31:S0;
!!VRv32:S0;
!!IF:V887/0;

!?BA&889;
!!IF:V886/1;

!?BR&886/889/v997=0;
!!DO3/0/7/1:P;
!!DO3/20/28/1:P; (loop through all stacks)
!!IF:V886/1;

!?FU3;
!!VRy1:S0;
!!VRy2:S0;
!!VRy3:S0;
!!BMx16:T?y1; (get type of creature)
!!BMx16&y1=34:N?y2; (get number if it was a mage stack)
!!BMx16&y2<>0:M29/99/2; (cast fire shield on themselves)