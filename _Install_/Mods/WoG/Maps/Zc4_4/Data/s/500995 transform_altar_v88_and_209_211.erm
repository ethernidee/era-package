ZVSE  
  
**   Tranformation Altar ERM Script Version 1.2 created by Timothy E. Pulver  
  
** This script lets all "Lightning Altars" on a map function as transformation  
** devices. When a hero climbs the altar, his or her army will be transformed  
** into different random creatures, while maintaining approximately the same  
** level of power. For example, a stack of 10 archers may become a stack of  
** 10 dwarves or 10 gargoyles instead. Powerful neutral dragons in the game  
** are immune to transformation, as are some of the new WoG creatures.  
** Each Transformation Altar on the map can only be visited once per week.  
  
** The "Lightning Altar" is a new WoG object that can be found in the WoG  
** Heroes map editor in the Town Objects section. It's the 5th object beneath  
** the Conflux castle. It appears as an altar with steps leading up to a pillar  
** with a light on top. From the light comes dancing lightning bolts.  
  
** To install this script in a map, simply copy & paste the code into a global  
** (timed) event. Copy the entire code, from ZVSE to the end. Make sure  
** that you disable ALL colours from receiving the event and/or change  
** the event day to something like 500 or later to avoid the ERM script  
** popping up as if it were a regular event message. The title of the event  
** is unimportant, but I would suggest something like "Transformation Altar".  
** And of course, you'll also need to put one or more "Lightning Altars" on  
** your map for the heroes to visit. Happy transforming! :-)  
  
  
Flags Used in this Script: 1,2,77, 209-211  
Variables Used in this Script: f,g,i,j,k,l,v88-v90,x16,z75,  
Functions Used in this Script: 18001  
  
!?OB63/20;  [Triggers when any "Lightning Altar" is visited]  
  
!!IF:V209/0 V210/0 V211/0;  [Sets flags 209, 210 and 211 to false]  
  
 [Set up right-click hint text for altar object]  
!!VRz75:S^Altar of Transformation^;  
!!OB998:H75;  
  
 [Ask if hero wishes to visit the altar]  
!!IF&1000:Q1^Do you climb the steps of the mysterious altar?^;  
  
 [If player is AI, generate random response]  
!!VRv90&-1000:S0 T1;  
!!IF&v90=0/-1000:V1/0;  
!!IF&v90=1/-1000:V1/1;  
  
 [Check if someone has already visited the altar this week. If noone has and hero visits, set control word to indicate that it has now been visited this week]  
!!IF&1:V77/0;      [Reset flag 77 to 0]  
!!OB998:C?v89;  [Get control word of this altar]  
!!OB998&v89>999:C0; [If altar hasn't been visited, set its control word to 0]  
!!VRv88:Sc0 -1 :7 *7 +1;  [Determine most recent Day 1]  
!!IF&v88<=v89/1:V77/1;  [If most recent Day 1 is earlier or the same as the  
                                        the day last visited, don't allow visit]  
!!OB998&1:Cc0;               [If visiting, set control word to current day]  
  
 [If altar has been visited this week, display this message and set flag 1 to false]  
!!IF&77/1/1000:M^You climb to the top of the altar but the energy merely sputters around you as if it had recently been drained. Try returning at the beginning of next week.^;  
!!IF&77:V1/0;  
  
 [If hero visits altar, display this message]  
!!IF&1/1000:M^{Flickering Energies}  
  
~~ CRACKLE ~~  
  
{all around you...}^;  
  
 [If hero visits altar, set v77 variable to true and make a "teleport hero" sound]  
!!IF&1:V77/1;  
!!HE-1&1/1000:Pv998/v999/v1000;  
  
 [If hero visits altar, perform transformation function 7 times -- once for each army slot]  
!!DO18001/0/6/1&1:P;  
  
 [After transformation, display this message plus second message if appropriate]  
!!IF&1/1000:M^You step down from the altar with  
your army transformed!^;  
!!IF&209/-211/1000:M^The powerful dragons in your army resist the transformation!^;  
!!IF&211/-209/1000:M^The strange new creatures in your army resist the transformation!^;  
!!IF&209/211/1000:M^The powerful dragons and the strange new creatures in your army resist the transformation!^;  
  
----------------------------------------------------------------------------------------  
  
 [Start of transformation function]  
!?FU18001;  
  
!!VRi:S-1;  [Sets variable i to -1]  
!!HE-1:C0/x16/?i/d0;  [Stores value of troop type found in hero's slot x16 in variable i]  
  
!!VRg:S0;   [Sets variable g to 0]  
!!VRf:S0;   [Sets variable f to 0]  
!!VRl:S-1;  [Sets variable l to -1]  
!!IF:V2/0 V210/0;  [Sets flag 2 and flag 210 to false]  
  
 [If existing troop type is a powerful neutral dragon, set flags 209 and 210 to 1]  
!!IF&i>131/i<136:V209/1 V210/1;  
  
!!VRg&i>101/i<104:S4; [If troop type is a gorgon or mighty gorgon, set variable g to 4]  
!!VRg&i>103/i<108:S-2; [If troop type is a serpent fly, dragon fly, basilisk or greater  basilisk, set variable g to -2]  
  
!!VRi&g<>0:+g; [If variable g does NOT equal 0, add value of g to variable i]  
!!IF&g<>0:V2/1;  [If variable g does NOT equal 0, set flag 2 to true]  
  
 [Sets Conflux plus neutral creatures temporary values that are in numerical order]  
!!VRf&i=118:S182;   [Pixie]  
!!VRf&i=139:S182;   [Peasant]  
!!VRf&i=119:S183;   [Sprite]  
!!VRf&i=138:S183;   [Halfling]  
!!VRf&i=112:S184;   [Air Elemental]  
!!VRf&i=140:S184;   [Boar Rider]  
!!VRf&i=127:S185;   [Storm Elemental]  
!!VRf&i=143:S185;   [Rogue]  
!!VRf&i=115:S186;   [Water Elemental]  
!!VRf&i=142:S186;   [Nomad]  
!!VRf&i=123:S187;   [Ice Elemental]  
!!VRf&i=141:S187;   [Mummy]  
!!VRf&i=114:S188;   [Fire Elemental]  
!!VRf&i=129:S189;   [Energy Elemental]  
!!VRf&i=116:S189;   [Gold Golem]  
!!VRf&i=137:S189;   [Sharpshooter]  
!!VRf&i=113:S190;   [Earth Elemental]  
!!VRf&i=125:S191;   [Magma Elemental]  
!!VRf&i=117:S191;   [Diamond Golem]  
!!VRf&i=144:S191;   [Troll]  
!!VRf&i=120:S192;   [Psychic Elemental]  
!!VRf&i=121:S193;   [Magic Elemental]  
!!VRf&i=136:S193;   [Enchanter]  
!!VRf&i=130:S194;   [Firebird]  
!!VRf&i=131:S195;   [Phoenix]  
  
!!VRj&i<112:S0 R8;  [If troop type is less than 112, set j to random # from 0 to 8]  
!!VRj&f>180:S0 R8;  [If troop type is greater than 180, set j to random # from 0 to 8]  
!!VRj&i<112/j=7/2:S0 R6;  [If troop type is less than 112 and j=8 and flag 2 is true, set j to a random # from 0 to 6]  
!!VRk&i<112:Si %14; [If troop type is less than 112, set k to the *remainder* of troop type divided by 14]  
!!VRk&i>149/i<159:S-1; [If troop type is "Level 8" creature, set k to -1]  
!!VRk&i>158/i<180:S-2; [If troop type is another obscure new creature, set k to -2]  
!!VRk&f>180:Sf %14; [If troop type is greater than 180, set k to the *remainder* of troop type divided by 14]  
!!VRl&k=-1:S150 R8; [If troop type is "Level 8" creature, set l (new troop type) to random new "Level 8" creature]  
!!VRl&k=-2:Si;  [If troop type is an obscure new creature, just leave it alone]  
!!IF&k=-2:V211/1; [If troop type is an obscure new creature, set flag 211 to true]  
  
!!VRl&j=8/k=0:S118; [If j=8 and k=0, set l (new troop type) to Pixie]  
!!VRl&j=8/k=1:S119; [If j=8 and k=1, set l (new troop type) to Sprite]  
!!VRl&j=8/k=2:S112; [If j=8 and k=2, set l (new troop type) to Air Elemental]  
!!VRl&j=8/k=3:S127; [If j=8 and k=3, set l (new troop type) to Storm Elemental]  
!!VRl&j=8/k=4:S115; [If j=8 and k=4, set l (new troop type) to Water Elemental]  
!!VRl&j=8/k=5:S123; [If j=8 and k=5, set l (new troop type) to Ice Elemental]  
!!VRl&j=8/k=6:S114; [If j=8 and k=6, set l (new troop type) to Fire Elemental]  
!!VRl&j=8/k=7:S129; [If j=8 and k=7, set l (new troop type) to Energy Elemental]  
!!VRl&j=8/k=8:S113; [If j=8 and k=8, set l (new troop type) to Earth Elemental]  
!!VRl&j=8/k=9:S125; [If j=8 and k=9, set l (new troop type) to Magma Elemental]  
!!VRl&j=8/k=10:S120; [If j=8 and k=10, set l (new troop type) to Psychic Elemental]  
!!VRl&j=8/k=11:S121; [If j=8 and k=11, set l (new troop type) to Magic Elemental]  
!!VRl&j=8/k=12:S130; [If j=8 and k=12, set l (new troop type) to Firebird]  
!!VRl&j=8/k=13:S131; [If j=8 and k=13, set l (new troop type) to Phoenix]  
  
 [If troop type is less than 112 or greater than 180 and j does NOT equal 8, set l (new troop type) to: (j x 14) + k ]  
!!VRl&i<112/j<>8:Sj *14 +k;  
!!VRl&f>180/j<>8:Sj *14 +k;  
  
 [Make adjustments to compensate for some of the Fortress creatures being out of order in table]  
!!VRg&-2/i<112/l>101/l<106:S2;  
!!VRg&-2/i<112/l>105/l<108:S-4;  
!!VRg&-2/f>180/l>101/l<106:S2;  
!!VRg&-2/f>180/l>105/l<108:S-4;  
!!VRl&-2/g<>0:+g;  
  
!!FU18001&i=l/k<>-2:P; [If current troop = new troops, choose another troop]  
!!VRi&f>180:Sf; [If f (temporary #) is greater than 180, set current troop type equal to f]  
  
 [Set new troop type in slot x16]  
!!HE-1&l<>-1/-210:C0/x16/l/d0;  