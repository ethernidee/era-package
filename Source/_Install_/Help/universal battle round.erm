ZVSE
; Script:   Universal Battle Rounds
; Version:  1.0
; Requires: Era 1.5+

; Uses 5 global v-vars: v100-v104. Feel free to change.
; v100 - real tactics phase will take place
; v101 - visible round ID
; v102 - real round ID (not counting fake and tactics rounds)
; v103 - [implementation specific, dont rely on it] zero round flag if tactics takes place
; v104 - [implementation specific, dont rely on it] is current BR trigger native or generated by script

!?BA0;
; y1-y2 - heroes numbers
; y3-y4 - heroes tactics
; y5-y6 - heroes tactics states
!!BA:H0/?y1 H1/?y2;
!!HEy1:S19/?y3 R4/?y5;
!!VRy4:S0;
!!HEy2&y2>=0:S19/?y4 R4/?y6;
!!VRv100:Sy3-y4;
!!VRv100&v100>0/y5=0:S0;
!!VRv100&v100<0/y6=0:S0;
!!VRv100&v100<>0:S1;
!!VRv101:S-1;
!!VRv102:S-1;
!!VRv103:Sv100;
!!VRv104:S1;

!?BR&v104=1;
!!VRv102&v997>=0/v103=1:+1;
!!if&v100=1:;
  !!VRv101&v997<>-1:+1;
!!el:;
  !!VRv101:+v103;
  !!VRv103&v103=0/v997=0:+1;
!!en:;

!?FU77006&v100=1/v997=-1/v101=-1;
!!VRv101:+1;
!!VRv104:S0;
!!FU30302:P;
!!VRv104:S1;

!?BR;
!!IF:M^Tactics takes/took place: %V100
WoG round ID: %V997
Visible round ID: %V101
Real round ID: %V102^;
